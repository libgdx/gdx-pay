apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate { project ->
    publishing {
        publications {
            release(MavenPublication) {
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                pom {
                    name = POM_NAME
                    if (project.hasProperty('POM_PACKAGING')) {
                        packaging = POM_PACKAGING
                    }
                    description = POM_DESCRIPTION
                    url = POM_URL
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }
                    developers {
                        rootProject.developers.each { dev ->
                            developer {
                                id = dev.id
                                name = dev.name
                                url = dev.url
                            }
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }
                }
            }
        }

        repositories {
            maven {
                if (isLocalBuild()) {
                    url = getLocalRepositoryUrl()
                } else if (isReleaseBuild()) {
                    url = getReleaseRepositoryUrl()
                    credentials {
                        username = getRepositoryUsername()
                        password = getRepositoryPassword()
                    }
                } else {
                    url = getSnapshotRepositoryUrl()
                    credentials {
                        username = getRepositoryUsername()
                        password = getRepositoryPassword()
                    }
                }
            }
        }
    }

    signing {
        if (isReleaseBuild()) {
            useGpgCmd()
        }
        required { isReleaseBuild() }
        sign publishing.publications
    }
}
