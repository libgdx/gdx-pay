task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.badlogicgames.gdxpay'
    version = '0.11.2-SNAPSHOT'
}

class Developer {
    String id
    String name
    String url
}

ext {
    androidBuildToolsVersion = '25.0.2' // version also referenced in .travis.yml.
    androidCompileSdkVersion = 24 // version also referenced in .travis.yml.
    androidMinimalSdkVersion = 9
    androidTargetSdkVersion = 24
    assertJVersion = '1.7.1'
    gdxVersion = '1.9.6'
    openIabVersion = '0.9.8.7'
    robolectricVersion = '4.3_r2-robolectric-0'

    developers = [
            new Developer(id: 'noblemaster', name: 'Christoph Aschwanden',
                    url: "http://www.noblemaster.com"),
            new Developer(id: 'keesvandieren', name: 'Kees van Dieren',
                    url: "https://www.teachkidslanguages.com")
    ]

    libraries = [
            assertj_core                            : "org.assertj:assertj-core:1.7.1",
            jsr305                                  : 'com.google.code.findbugs:jsr305:3.0.2',
            junit                                   : 'junit:junit:4.12',
            libgdx_core                             : "com.badlogicgames.gdx:gdx:${gdxVersion}",
            libgdx_backend_android                  : "com.badlogicgames.gdx:gdx-backend-android:${gdxVersion}",
            libgdx_backend_headless                 : "com.badlogicgames.gdx:gdx-backend-headless:${gdxVersion}",
            libgdx_backend_lwjgl                    : "com.badlogicgames.gdx:gdx-backend-lwjgl:${gdxVersion}",
            libgdx_backend_lwjgl3                   : "com.badlogicgames.gdx:gdx-backend-lwjgl3:${gdxVersion}",
            libgdx_backend_robovm                   : "com.badlogicgames.gdx:gdx-backend-robovm:${gdxVersion}",
            libgdx_freetype                         : "com.badlogicgames.gdx:gdx-freetype:${gdxVersion}",
            libgdx_freetype_platform_natives_desktop: "com.badlogicgames.gdx:gdx-freetype-platform:${gdxVersion}:natives-desktop",
            libgdx_platform_natives_armeabi         : "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-armeabi",
            libgdx_platform_natives_armeabi_v7a     : "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-armeabi-v7a",
            libgdx_platform_natives_arm64_v8a       : "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-arm64-v8a",
            libgdx_platform_natives_desktop         : "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-desktop",
            libgdx_platform_natives_ios             : "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-ios",
            libgdx_platform_natives_x86             : "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-x86",
            libgdx_platform_natives_x86_64          : "com.badlogicgames.gdx:gdx-platform:${gdxVersion}:natives-x86_64",
            libgdx_tools                            : "com.badlogicgames.gdx:gdx-tools:${gdxVersion}",
            mockito_core                            : 'org.mockito:mockito-core:2.7.21',
            robovm_rt                               : "com.mobidevelop.robovm:robovm-rt:$roboVMVersion",
            robovm_cocoatouch                       : "com.mobidevelop.robovm:robovm-cocoatouch:$roboVMVersion",
            robovm_objc                             : "com.mobidevelop.robovm:robovm-cocoatouch:$roboVMVersion",
            support_v4                              : "com.android.support:support-v4:25.0.0"
    ]

}

buildscript {
    ext {
        androidToolsVersion = '2.3.1'
        roboVMVersion = '2.3.0'
        moeVersion = '1.3.6'
        unmockVersion = "0.3.6"
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${androidToolsVersion}"
        classpath "com.mobidevelop.robovm:robovm-gradle-plugin:${roboVMVersion}"
        classpath "org.multi-os-engine:moe-gradle:${moeVersion}"
        classpath "de.mobilej.unmock:UnMockPlugin:${unmockVersion}"
    }
}

allprojects {
	  apply plugin: "eclipse"
    
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":gdx-pay-tests-iosmoe") {
    apply plugin: "moe"
    configurations { natives }
    dependencies {
        compile project(':gdx-pay-client')
        compile project(':gdx-pay-tests')
        compile project(':gdx-pay-iosmoe-apple')
        compile "com.badlogicgames.gdx:gdx-backend-moe:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
